buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.gradle.build-scan' version '1.0'
    id 'net.researchgate.release' version '2.4.0'
    id "com.github.samueltbrown.cucumber" version "0.9"
    id "com.coverity.ondemand" version "1.5.61"
    id "com.sourcemuse.mongo" version "0.13.0"
}

apply plugin: 'spring-boot'

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.data:spring-data-mongodb'
    compile 'org.springframework.social:spring-social-facebook'
    compile 'com.google.guava:guava:19.0'
    compile 'net.sf.oval:oval:1.85'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'info.cukes:cucumber-junit:1.2.4'
    testCompile 'info.cukes:cucumber-java:1.2.4'

    cucumberCompile 'info.cukes:cucumber-java:1.2.4'
}

def mongoRunning = false

test {
    forkEvery = 5
    maxParallelForks = Runtime.runtime.availableProcessors() / 2

    doFirst {
        startMongoDb.execute()
        mongoRunning = true
    }

    finalizedBy stopMongoDb
}

stopMongoDb{
    onlyIf {mongoRunning}
    doLast  {
        mongoRunning = false
    }
}

jar {
    baseName = 'carminp-nosql-api'
    version = project.version
    destinationDir = file('target/')
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

sourceSets {
    test {
        java {
            srcDirs 'src/test/java'
        }
    }
}

release {
    preTagCommitMessage = 'release(tag) - pre tag commit: '
    tagCommitMessage = 'release(tag) - creating tag: '
    newVersionCommitMessage = 'release(tag) - new version commit: '
    tagTemplate = '${version}'

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

cucumber {
    formats = ['pretty','json:build/cucumber.json','junit:build/cucumber.xml']
}

eclipse.classpath.file {
    beforeMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' || entry.kind == 'var' }
    }
}

bootRun {
    systemProperties System.properties
}

mongo {
    port 12345
    logging 'console'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}